plugins {
    id 'de.dfki.mary.voicebuilding-base' version '5.2.1'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'

repositories {
    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/'
    }
}

voice {
    name = 'amu-waldek-hsmm'
    gender = 'male'
    language = 'pl'
    type = 'hsmm'
    description = 'A male Polish hidden semi-Markov model voice, built from voice recordings provided by AMU Poznan'
    license {
        name = 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International'
        shortName = 'CC-BY-NC-SA'
        url = 'http://mary.dfki.de/download/by-nc-sa-4.0.html'
    }
    samplingRate = 16000
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name == 'marytts-lang-pl') {
            details.useTarget group: details.requested.group, name: details.requested.name, version: '0.1.0-SNAPSHOT'
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
